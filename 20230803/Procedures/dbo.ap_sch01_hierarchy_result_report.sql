SET ANSI_NULLS ON
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE dbo.ap_sch01_hierarchy_result_report 
(
    @HierarchyBusinessUrn int, -- the business urn of the hierarchy to report on
    @DaysInPast int   -- number of days that firetime must be between
) AS
    SELECT 
        v.NAME, 
        0 AS REFIRE_COUNT, 
        CASE at.INTERNAL_NAME
        WHEN 'AT_HIERARCHY_CHANGE_REQUESTED' THEN
        CASE audit.EVENT_OUTCOME
                WHEN 'S' THEN 'Update Request Success [' + CAST(hc.HIERARCHY_CHANGE_ID AS NVARCHAR(100)) + ']'
                WHEN 'F' THEN 'Update Request Failure [' + CAST(hc.HIERARCHY_CHANGE_ID AS NVARCHAR(100)) + '] :' + audit.EVENT_RAW_DESCRIPTION
                ELSE NULL END
        ELSE
            CASE audit.EVENT_OUTCOME
                WHEN 'S' THEN 'Update Execution Success [' + CAST(hc.HIERARCHY_CHANGE_ID AS NVARCHAR(100)) + ']'
                WHEN 'F' THEN 'Update Execution Failure [' + CAST(hc.HIERARCHY_CHANGE_ID AS NVARCHAR(100)) + '] :' + audit.EVENT_RAW_DESCRIPTION
                ELSE NULL END
        END AS MESSAGE,
        audit.EVENT_OUTCOME AS RESULT, 
        audit.EVENT_UTC_DATE_TIME AS FIRETIME, 
        hc.EFFECTIVE_FROM as SESSION_DATE,
        CAST(CASE SUM((CASE WHEN r.HIERARCHY_CHANGE_ID IS NULL THEN 0 ELSE 1 END)) WHEN 0 THEN 0 ELSE 1 END AS BIT) AS HISTORICAL,
        CASE WHEN audit.EVENT_OUTCOME = 'F' THEN NULL ELSE COUNT(r.HIERARCHY_CHANGE_ID) END as DIMENSION_CHANGE_COUNT,            
        CASE WHEN cri.RESOURCE_INSTANCE_ID IS NULL OR audit.EVENT_OUTCOME = 'F' THEN NULL ELSE CASE cri.IS_PUBLISHER WHEN 1 THEN 'Primary Database' ELSE 'Secondary Database' END END AS FACT_UPDATED_ON,
        COALESCE(CASE WHEN audit.EVENT_OUTCOME = 'F' THEN NULL ELSE 
            CASE SUM((CASE WHEN r.HIERARCHY_CHANGE_ID IS NULL THEN 0 ELSE 1 END)) WHEN 0 THEN NULL ELSE COALESCE(fr.FACT_UPDATE_STATUS, 'P') END END, 
            'N') AS FACT_UPDATE_STATUS,
        CASE fr.FACT_UPDATE_STATUS WHEN 'C' THEN 2 WHEN 'H' THEN 1 ELSE 0 END AS FACT_UPDATE_COUNT
    FROM dbo.VW_FCT_AUDIT audit
    JOIN dbo.TE_ADM_AUDIT_TYPE at ON audit.AUDIT_TYPE_ID = at.AUDIT_TYPE_ID AND at.INTERNAL_NAME IN ('AT_HIERARCHY_CHANGE_REQUESTED', 'AT_HIERARCHY_CHANGE_COMMITTED')
    JOIN TB_SCH_HIERARCHY_CHANGE hc ON hc.HIERARCHY_CHANGE_ID = audit.GUID_VAL_1
    JOIN VW_DIM_HIERARCHY v ON hc.HIERARCHY_BUSINESS_URN = v.ITEM_BIZ_URN
    LEFT JOIN dbo.TB_SCH_HIERARCHY_CHANGE_RESULT r on hc.HIERARCHY_CHANGE_ID = r.HIERARCHY_CHANGE_ID
    LEFT JOIN dbo.TB_SCH_HIERARCHY_CHANGE_FACT_UPDATE_RESULT fr on fr.HIERARCHY_CHANGE_RESULT_ID = r.HIERARCHY_CHANGE_RESULT_ID    
    LEFT JOIN dbo.TB_CLU_RESOURCE_INSTANCE AS cri ON cri.RESOURCE_INSTANCE_ID = fr.RESOURCE_INSTANCE_ID
    WHERE v.ITEM_BIZ_URN = @HierarchyBusinessUrn
    AND audit.EVENT_UTC_DATE_TIME BETWEEN DATEADD(d, -@DaysInPast, GETUTCDATE()) AND GETUTCDATE()
    GROUP BY v.NAME, hc.HIERARCHY_CHANGE_ID, at.INTERNAL_NAME, audit.EVENT_RAW_DESCRIPTION,audit.EVENT_OUTCOME, audit.EVENT_UTC_DATE_TIME, hc.EFFECTIVE_FROM, cri.RESOURCE_INSTANCE_ID, cri.IS_PUBLISHER, fr.FACT_UPDATE_STATUS
    ORDER BY audit.EVENT_UTC_DATE_TIME ASC;
GO
GRANT EXECUTE
	ON [dbo].[ap_sch01_hierarchy_result_report]
	TO [portalapp_role]
GO
